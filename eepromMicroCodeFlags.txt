#define SHIFT_DATA 2
#define SHIFT_CLK 3
#define SHIFT_LATCH 4
#define EEPROM_D0 5
#define EEPROM_D7 12
#define WRITE_EN 13

//defining the control signals 
#define HLT 0b1000000000000000 //Halt clock/computer
#define MI  0b0100000000000000 //Mem address reg in
#define RI  0b0010000000000000 //Ram in
#define RO  0b0001000000000000 //Ram out
#define IO  0b0000100000000000 //Instr Reg out
#define II  0b0000010000000000 //Instr Reg in
#define AI  0b0000001000000000 //A reg in
#define AO  0b0000000100000000 //A reg out
#define EO  0b0000000010000000 //Sum out
#define SU  0b0000000001000000 //Subtract Bit
#define BI  0b0000000000100000 //B reg in
#define OI  0b0000000000010000 //(hex display) Output reg in
#define CE  0b0000000000001000 //Counter enable
#define CO  0b0000000000000100 //Counter out
#define J   0b0000000000000010 //Jump (counter in)
#define FI  0b0000000000000001 // Flags in

#define FLAGS_Z0C0 0
#define FLAGS_Z0C1 1
#define FLAGS_Z1C0 2
#define FLAGS_Z1C1 3

#define JC  0b0111
#define JZ  0b1000

//Setting up the instructions using control signals
//This creates the steps for processing an instruction
uint16_t UCODE_TEMPLATE[16][8] = {
//T0     T1        T2     T3     T4        T5 T6 T7
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //0000 - NOP
  {MI|CO, RO|II|CE, IO|MI, RO|AI, 0,           0, 0, 0}, //0001 - LDA
  {MI|CO, RO|II|CE, IO|MI, RO|BI, EO|AI|FI,    0, 0, 0}, //0010 - ADD
  {MI|CO, RO|II|CE, IO|MI, RO|BI, EO|AI|SU|FI, 0, 0, 0}, //0011 - SUB
  {MI|CO, RO|II|CE, IO|MI, AO|RI, 0,           0, 0, 0}, //0100 - STA
  {MI|CO, RO|II|CE, IO|AI, 0,     0,           0, 0, 0}, //0101 - LDI
  {MI|CO, RO|II|CE, IO|J,  0,     0,           0, 0, 0}, //0110 - JMP
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //0111 - JC
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1000 - JZ
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1001 - 
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1010 - 
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1011 - 
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1100 - 
  {MI|CO, RO|II|CE, 0,     0,     0,           0, 0, 0}, //1101 - 
  {MI|CO, RO|II|CE, AO|OI, 0,     0,           0, 0, 0}, //1110 - OUT
  {MI|CO, RO|II|CE, HLT,   0,     0,           0, 0, 0}, //1111 - HLT
};

uint16_t ucode[4][16][8];

void initUCode() {
  //ZF = 0, CF = 0
  memcpy(ucode[FLAGS_Z0C0], UCODE_TEMPLATE, sizeof(UCODE_TEMPLATE));

  //ZF = 0, CF = 1
  memcpy(ucode[FLAGS_Z0C1], UCODE_TEMPLATE, sizeof(UCODE_TEMPLATE));
  ucode[FLAGS_Z0C1][JC][2] = IO|J;

  //ZF = 1, CF = 0
  memcpy(ucode[FLAGS_Z1C0], UCODE_TEMPLATE, sizeof(UCODE_TEMPLATE));
  ucode[FLAGS_Z1C0][JZ][2] = IO|J;

  //ZF = 1, CF = 1
  memcpy(ucode[FLAGS_Z1C1], UCODE_TEMPLATE, sizeof(UCODE_TEMPLATE));
  ucode[FLAGS_Z1C1][JC][2] = IO|J;
  ucode[FLAGS_Z1C1][JZ][2] = IO|J;
}


void setAddress(int address, bool outputEnable){
  shiftOut(SHIFT_DATA, SHIFT_CLK, MSBFIRST, (address >> 8) | (outputEnable ? 0x00 : 0x80));
  shiftOut(SHIFT_DATA, SHIFT_CLK, MSBFIRST, address);

  digitalWrite(SHIFT_LATCH, LOW);
  digitalWrite(SHIFT_LATCH, HIGH);
  digitalWrite(SHIFT_LATCH, LOW);
}

byte readEEPROM(int address){
  for (int pin = EEPROM_D0; pin <= EEPROM_D7; pin += 1){
      pinMode(pin, INPUT);
  }

  setAddress(address, /*outputEnable*/ true);
  byte data = 0;
  for (int pin = EEPROM_D7; pin >= EEPROM_D0; pin -= 1){
    data = (data << 1) + digitalRead(pin);
  }
  return data;
}

void writeEEPROM(int address, byte data){
  for (int pin = EEPROM_D0; pin <= EEPROM_D7; pin += 1){
      pinMode(pin, OUTPUT);
  }

  setAddress(address, /*outputEnable*/ false);
  for (int pin = EEPROM_D0; pin <= EEPROM_D7; pin += 1){
    digitalWrite(pin, data & 1);
    data = data >> 1;
  }
  digitalWrite(WRITE_EN, LOW);
  delayMicroseconds(1);
  digitalWrite(WRITE_EN, HIGH);
  delay(10);
}

void printContents(int start, int length){
  for (int base = start; base < length; base += 16){
    byte data[16];
    for(int offset = 0; offset <= 15; offset += 1){
      data[offset] = readEEPROM(base + offset);
    }
    char buf[80];
    sprintf(buf, "%03x:  %02x %02x %02x %02x %02x %02x %02x %02x    %02x %02x %02x %02x %02x %02x %02x %02x", 
      base, data[0], data[1], data[2], data[3], data[4], data[5], data[6], data[7], data[8], data[9], data[10], data[11], data[12], data[13], data[14], data[15]);
    
    Serial.println(buf);
  }
}

void setup() {
  initUCode();

  pinMode(SHIFT_DATA, OUTPUT);
  pinMode(SHIFT_CLK, OUTPUT);
  pinMode(SHIFT_LATCH, OUTPUT);
  digitalWrite(WRITE_EN, HIGH);
  pinMode(WRITE_EN, OUTPUT);
  Serial.begin(9600);
  
  // Program data bytes
  Serial.print("Programming EEPROM");

  // Program the 8 high-order bits of microcode into the first 128 bytes of EEPROM
  for (int address = 0; address < 1024; address += 1) {
    int flags       = (address & 0b1100000000) >> 8;
    int byte_sel    = (address & 0b0010000000) >> 7;
    int instruction = (address & 0b0001111000) >> 3;
    int step        = (address & 0b0000000111);

    if (byte_sel) {
      writeEEPROM(address, ucode[flags][instruction][step]);
    } else {
      writeEEPROM(address, ucode[flags][instruction][step] >> 8);
    }

    if (address % 64 == 0) {
      Serial.print(".");
    }
  }

  Serial.println(" done");


  // Read and print out the contents of the EERPROM
  Serial.println("Reading EEPROM");
  printContents(0, 1024);
}


void loop() {

}
